plugins {
    id 'application'
    id "org.sonarqube" version "3.0"
    id 'org.springframework.boot' version '2.7.13'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.owasp.dependencycheck' version '8.0.2'
    id "uk.gov.hmcts.java" version '0.12.43'
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
    id 'jacoco'
}

def springBootVersion = '2.7.13'
def springCloudVersion = '2021.0.8'


def versions = [
        lombok: '1.18.28',
        javaVersion: '17',
        jacocoVersion: '0.8.5'
]

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

dependencies{
    testImplementation 'com.github.hmcts:fortify-client:1.3.0:all'
}

task fortifyScan(type: JavaExec)  {
    main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

allprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'idea'
    apply plugin: 'jacoco'

    group = 'uk.gov.hmcts.reform.payment'
    ext {
        buildNumber = System.getenv("BUILD_NUMBER") ?: '0'
    }
    version = rootProject.file('version.txt').text.trim() + ".${buildNumber}"

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'https://repo.spring.io/libs-milestone' }
        maven { url 'https://jitpack.io' }
    }

    apply from: "${rootDir}/cve-resolution-strategy.gradle"

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom 'org.apache.logging.log4j:log4j-bom:2.17.1'
        }
        dependencies {
            dependencySet(
                    group: 'com.fasterxml.jackson.core',
                    version: '2.15.2'
            ) {
                entry 'jackson-core'
                entry 'jackson-databind'
            }
        }
    }


    tasks.register("codeCoverageReport", JacocoReport) {
        // If a subproject applies the 'jacoco' plugin, add the result it to the report
        subprojects { subproject ->
            subproject.plugins.withType(JacocoPlugin).configureEach {
                subproject.tasks.matching({ t -> t.extensions.findByType(JacocoTaskExtension) }).configureEach {
                    testTask ->
                        if (testTask.getTaskIdentity().name == "test") {
                            sourceSets subproject.sourceSets.main
                            executionData(testTask)
                        }
                }
            }
        }
        // enable the different report types (html, xml, csv)
        reports {
            // xml is usually used to integrate code coverage with
            // other tools like SonarQube, Coveralls or Codecov
            xml.enabled true
            // HTML reports can be used to see code coverage
            // without any external tools
            html.enabled true
            xml.destination = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
        }
    }

    codeCoverageReport.dependsOn {
        subprojects*.test
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: "jacoco"

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Test) {
        testLogging {
            exceptionFormat = 'full'
        }
    }
    dependencies {
        implementation ('com.sun.xml.bind:jaxb-osgi:2.3.8')
        testImplementation 'io.github.openfeign:feign-jackson:12.3'
        implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.15.2'
        implementation group: 'joda-time', name: 'joda-time', version: '2.10.5'
        implementation group: 'org.jboss.aerogear', name: 'aerogear-otp-java', version:'1.0.0'
        implementation group:'org.apache.poi', name: 'poi-ooxml', version: '5.2.3'
        implementation group: 'com.github.hmcts', name: 'properties-volume-spring-boot-starter', version: '0.1.1'
        testImplementation group: 'junit', name: 'junit', version: '4.12'
        testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.8.0'
        testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.3.3'
        compileOnly group: 'net.sourceforge.findbugs', name: 'annotations', version: '1.3.2'
        implementation group: 'org.owasp.encoder', name: 'encoder', version: '1.2.3'
        implementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.27.2'
        compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation(group: 'com.github.hmcts', name: 'ccd-client', version: '4.9.2') {
            sourceSets {
                main {
                    java {
                        exclude '**/CoreCaseDataClientAutoConfiguration.java'
                    }
                }
            }
        }

    }

    test {
        jvmArgs = [
            '--add-opens', 'java.base/java.lang=ALL-UNNAMED'
        ]
    }
}

dependencies {
    implementation project(':payment-api')
}

mainClassName = 'uk.gov.hmcts.PaymentApiApplication'

jar {
    enabled = true
}

bootJar {
    archiveName 'payment-app.jar'
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

springBoot {
    buildInfo()
}

dependencyCheck {
    failBuildOnCVSS = 0
    suppressionFile = 'dependency-check-suppressions.xml'
    format='ALL'
}

def sonarExclusions = [
        '**/v1/**',
        '**/OtpBootstrap.java',
        '**/api/model/**',
        '**/referencedata/model/**',
        '**/api/dto/*.java',
        '**/referencedata/dto/*.java',
        '**/api/service/*Service.java',
        '**/api/servicebus/TopicClientService.java',
        '**/referencedata/service/*Service.java',
        '**/api/service/MockAccountServiceImpl.java',
        '**/api/service/MockCcdDataStoreClientServiceImpl.java',
        '**/api/controllers/mock/MockCallbackControllerForTesting.java',
        '**/api/controllers/ServiceRequestController.java',
        '**/api/domain/mapper/**',
        '**/api/dto/mapper/*Mapper.java',
        '**/api/domain/service/**',
        '**/api/contract/**',
        '**/api/contract/*Dto.java',
        '**/api/dto/mapper/**',
        '**/api/reports/config/**',
        '**/api/external/client/dto/**',
        '**/api/external/client/exceptions/**',
        '**/api/jpaaudit/listner/**',
        '**/api/jpaaudit/model/**',
        '**/api/service/PaymentRefundsServiceImpl.java',
        '**/api/service/RefundRemissionEnableServiceImpl.java'
]

sonarqube {
    properties {
        property "sonar.host.url", "https://sonar.reform.hmcts.net/"
        property "sonar.dynamicAnalysis", "reuseReports"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.dependencyCheck.reportPath", "${project.buildDir}/reports/dependency-check-report.xml"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.exclusions", sonarExclusions.join(", ")
        property "sonar.test.exclusions", sonarExclusions.join(", ")
        property "sonar.coverage.exclusions", sonarExclusions.join(", ")
        property "sonar.projectName", "payment-app"
        property "sonar.projectKey", "uk.gov.hmcts.reform.payment:payment-app"
    }
}

project.tasks['sonarqube'].dependsOn codeCoverageReport

tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

task smoke(type: GradleBuild, description: 'Runs smoke tests', group: 'Verification') {
    buildFile = './api/build.gradle'
    tasks = ['smokeTest']
}

task functional(type: GradleBuild, description: 'Runs Functional tests', group: 'Verification') {
    buildFile = './api/build.gradle'
    tasks = ['functionalTest']
}

task migratePostgresDatabase(type: GradleBuild, description: 'Migrates Postgres Database', group: 'liquibase') {
    //forward projectProperties
    startParameter.projectProperties = project.gradle.startParameter.projectProperties
    buildFile = './api/build.gradle'
    tasks = ['migratePostgresDatabase']
}

task printVersion {
    description 'Prints the version of this project'
    doLast {
        print project.version
    }
}

run {
    def debug = System.getProperty("debug")
    if (debug == 'true') {
        jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005']
    }
}
