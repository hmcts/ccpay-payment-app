plugins {
    id 'application'
    id "org.sonarqube" version "2.6.2"
    id 'org.springframework.boot' version '2.1.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.5.RELEASE'
    id 'org.owasp.dependencycheck' version '3.3.4'
    id "com.gorylenko.gradle-git-properties" version "1.4.21"
    id 'findbugs'
    id 'jacoco'
}

def springBootVersion = '2.1.2.RELEASE'
def springCloudVersion = 'Finchley.SR2'

allprojects {
    apply plugin: 'maven'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'idea'

    group = 'uk.gov.hmcts.reform.payment'
    ext {
        buildNumber = System.getenv("BUILD_NUMBER") ?: '0'
    }
    version = rootProject.file('version.txt').text.trim() + ".${buildNumber}"

    repositories {
        mavenLocal()
        maven { url "https://dl.bintray.com/hmcts/hmcts-maven" }
        jcenter()
        maven { url 'https://repo.spring.io/libs-milestone' }
    }
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: "jacoco"
    apply plugin: "findbugs"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    dependencies {
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.9.8'
        compile group: 'joda-time', name: 'joda-time', version: '2.9.9'
        compile group: 'org.jboss.aerogear', name: 'aerogear-otp-java', version:'1.0.0'
        compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version: '0.0.4'
        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.assertj', name: 'assertj-core', version: '3.8.0'
        compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.4'
        compileOnly group: 'net.sourceforge.findbugs', name: 'annotations', version: '1.3.2'
        compile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.5.1'
    }

    test {
        finalizedBy jacocoTestReport
        jacoco {
            append = true
            destinationFile = file("${project.rootDir}/build/jacoco/test.exec")
        }
    }

    jacocoTestReport {
        executionData(test)
        reports {
            xml.enabled true
            html.enabled true
        }
    }

    findbugs {
        ignoreFailures = false
        reportsDir = file("$project.buildDir/reports/findbugs")
        effort = "max"
        reportLevel = "high"
        includeFilter = file("$rootProject.projectDir/findbugs-security-include.xml")
        excludeFilter = file("$rootProject.projectDir/findbugs-security-exclude.xml")
    }
}

dependencies {
    compile project(':payment-api')
}

mainClassName = 'uk.gov.hmcts.PaymentApiApplication'

jar {
    enabled = true
}

bootJar {
    archiveName 'payment-app.jar'
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

springBoot {
    buildInfo()
}

dependencyCheck {
    failBuildOnCVSS = 0
    suppressionFile = 'dependency-check-suppressions.xml'
    format='ALL'
}

def sonarExclusions = [
        '**/v1/**',
        '**/OtpBootstrap.java',
        '**/api/model/**',
        '**/api/dto/*.java',
        '**/api/service/*Service.java',
         '**/api/contract/**',
         '**/api/dto/mapper/**',
         '**/api/reports/config/**',
         '**/api/external/client/dto/**',
         '**/api/external/client/exceptions/**'
]

sonarqube {
    properties {
        property "sonar.host.url", "https://sonar.reform.hmcts.net/"
        property "sonar.dynamicAnalysis", "reuseReports"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.jacoco.reportPaths", "${project.buildDir}/jacoco/test.exec"
        property "sonar.dependencyCheck.reportPath", "${project.buildDir}/reports/dependency-check-report.xml"
        property "sonar.exclusions", sonarExclusions.join(", ")
        property "sonar.test.exclusions", sonarExclusions.join(", ")
        property "sonar.coverage.exclusions", sonarExclusions.join(", ")
    }
}

task smoke(type: GradleBuild, description: 'Runs smoke tests', group: 'Verification') {
    buildFile = './api/build.gradle'
    tasks = ['smokeTest']
}

task functional(type: GradleBuild, description: 'Runs Functional tests', group: 'Verification') {
    buildFile = './api/build.gradle'
    tasks = ['functionalTest']
}

task migratePostgresDatabase(type: GradleBuild, description: 'Migrates Postgres Database', group: 'liquibase') {
    //forward projectProperties
    startParameter.projectProperties = project.gradle.startParameter.projectProperties
    buildFile = './api/build.gradle'
    tasks = ['migratePostgresDatabase']
}

task printVersion {
    description 'Prints the version of this project'
    doLast {
        print project.version
    }
}

run {
    def debug = System.getProperty("debug")
    if (debug == 'true') {
        jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005']
    }
}

