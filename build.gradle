plugins {
    id 'application'
    id "org.sonarqube" version "6.2.0.5505"
    id 'org.springframework.boot' version '3.3.12'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.owasp.dependencycheck' version '12.1.3'
    id "uk.gov.hmcts.java" version '0.12.66'
    id "com.gorylenko.gradle-git-properties" version "2.5.0"
    id 'jacoco'
}

def springBootVersion = '3.3.13'
def springCloudVersion = '2023.0.6'

def versions = [
        lombok: '1.18.38',
        javaVersion: '21',
        jacocoVersion: '0.8.12'
]

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

dependencies{
    testImplementation 'com.github.hmcts:fortify-client:1.4.9:all'
}

task fortifyScan(type: JavaExec)  {
    mainClass = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

allprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'idea'
    apply plugin: 'jacoco'

    group = 'uk.gov.hmcts.reform.payment'
    ext {
        buildNumber = System.getenv("BUILD_NUMBER") ?: '0'
    }
    version = rootProject.file('version.txt').text.trim() + ".${buildNumber}"

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = 'https://repo.spring.io/libs-milestone' }
        maven { url = 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1' }
    }

    apply from: "${rootDir}/cve-resolution-strategy.gradle"

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "org.springframework:spring-framework-bom:6.1.18"
            mavenBom 'org.apache.logging.log4j:log4j-bom:2.23.1'
            mavenBom 'io.netty:netty-bom:4.1.118.Final'
        }
        dependencies {
            dependencySet(
                    group: 'com.fasterxml.jackson.core',
                    version: '2.18.0'
            ) {
                entry 'jackson-core'
                entry 'jackson-databind'
            }
            // CVE-2020-8908
            dependencySet(group: 'com.google.guava', version: '33.4.8-jre') {
                entry 'guava'
            }

            dependencySet(group: 'ch.qos.logback', version: '1.5.18') {
                entry 'logback-core'
                entry 'logback-classic'
            }
        }
    }

    configurations {
        testCompile.exclude group: 'pull-parser', module: 'pull-parser'
        testRuntime.exclude group: 'ch.qos.logback', module: 'logback-classic', version: '1.5.8'
        testRuntime.exclude group: 'ch.qos.logback', module: 'logback-core', version: '1.5.8'
        testCompile.exclude group: 'ch.qos.logback', module: 'logback-classic', version: '1.5.8'
        testCompile.exclude group: 'ch.qos.logback', module: 'logback-core', version: '1.5.8'
    }


    tasks.register("codeCoverageReport", JacocoReport) {
        // If a subproject applies the 'jacoco' plugin, add the result it to the report
        subprojects { subproject ->
            subproject.plugins.withType(JacocoPlugin).configureEach {
                subproject.tasks.matching({ t -> t.extensions.findByType(JacocoTaskExtension) }).configureEach {
                    testTask ->
                        if (testTask.getTaskIdentity().name == "test") {
                            sourceSets subproject.sourceSets.main
                            executionData(testTask)
                        }
                }
            }
        }
        // enable the different report types (html, xml, csv)
        reports {
            // xml is usually used to integrate code coverage with
            // other tools like SonarQube, Coveralls or Codecov
            xml.required.set(true)
            // HTML reports can be used to see code coverage
            // without any external tools
            html.required.set(true)
            xml.outputLocation.set(file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"))
        }
    }

    codeCoverageReport.dependsOn {
        subprojects*.test
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: "jacoco"

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-parameters'
    }

    tasks.withType(Test).configureEach {
        testLogging {
            exceptionFormat = 'full'
        }
    }

    dependencies {
        implementation ('com.sun.xml.bind:jaxb-osgi:4.0.5')
        testImplementation 'io.github.openfeign:feign-jackson:13.2.1'
        testImplementation 'io.github.openfeign:feign-micrometer:13.5'
        implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.17.1'
        implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version:'2.17.1'
        implementation group: 'joda-time', name: 'joda-time', version: '2.13.1'
        implementation group: 'org.jboss.aerogear', name: 'aerogear-otp-java', version:'1.0.0'
        implementation group:'org.apache.poi', name: 'poi-ooxml', version: '5.4.1'
        testImplementation group: 'junit', name: 'junit', version: '4.13.2'
        testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.26.3'
        testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.11.0'
        compileOnly group: 'net.sourceforge.findbugs', name: 'annotations', version: '1.3.2'
        implementation group: 'org.owasp.encoder', name: 'encoder', version: '1.2.3'
        testImplementation group: 'com.github.tomakehurst', name: 'wiremock-jre8', version: '3.0.1'
        compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.18.3'
        implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.18.3'
        implementation(group: 'com.github.hmcts', name: 'core-case-data-store-client', version: '5.1.1') {
            sourceSets {
                main {
                    java {
                        exclude '**/CoreCaseDataClientAutoConfiguration.java'
                    }
                }
            }
        }

    }

    test {
        jvmArgs = [
            '--add-opens', 'java.base/java.lang=ALL-UNNAMED'
        ]
    }
}

dependencies {
    implementation project(':payment-api')
}

application  {
    mainClass = 'uk.gov.hmcts.PaymentApiApplication'
}

jar {
    enabled = true
}

bootJar {
    archiveFileName.set('payment-app.jar')
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

springBoot {
    buildInfo()
}

dependencyCheck {
    failBuildOnCVSS = 0
    suppressionFile = 'dependency-check-suppressions.xml'
    format='ALL'
}

def sonarExclusions = [
        '**/v1/**',
        '**/OtpBootstrap.java',
        '**/api/model/**',
        '**/referencedata/model/**',
        '**/api/dto/*.java',
        '**/referencedata/dto/*.java',
        '**/api/service/*Service.java',
        '**/api/servicebus/TopicClientService.java',
        '**/referencedata/service/*Service.java',
        '**/api/service/MockAccountServiceImpl.java',
        '**/api/service/MockCcdDataStoreClientServiceImpl.java',
        '**/api/controllers/mock/MockCallbackControllerForTesting.java',
        '**/api/controllers/ServiceRequestController.java',
        '**/api/domain/mapper/**',
        '**/api/dto/mapper/*Mapper.java',
        '**/api/domain/service/**',
        '**/api/contract/**',
        '**/api/contract/*Dto.java',
        '**/api/dto/mapper/**',
        '**/api/reports/config/**',
        '**/api/external/client/dto/**',
        '**/api/external/client/exceptions/**',
        '**/api/jpaaudit/listner/**',
        '**/api/jpaaudit/model/**',
        '**/api/service/PaymentRefundsServiceImpl.java',
        '**/api/service/RefundRemissionEnableServiceImpl.java'
]

sonarqube {
    properties {
        property "sonar.host.url", "https://sonar.reform.hmcts.net/"
        property "sonar.dynamicAnalysis", "reuseReports"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.dependencyCheck.reportPath", "${project.buildDir}/reports/dependency-check-report.xml"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.exclusions", sonarExclusions.join(", ")
        property "sonar.test.exclusions", sonarExclusions.join(", ")
        property "sonar.coverage.exclusions", sonarExclusions.join(", ")
        property "sonar.projectName", "payment-app"
        property "sonar.projectKey", "uk.gov.hmcts.reform.payment:payment-app"
    }
}

project.tasks['sonarqube'].dependsOn codeCoverageReport

tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

task smoke(type: GradleBuild, description: 'Runs smoke tests', group: 'Verification') {
    buildFile = './api/build.gradle'
    tasks = ['smokeTest']
}

task functional(type: GradleBuild, description: 'Runs Functional tests', group: 'Verification') {
    buildFile = './api/build.gradle'
    tasks = ['functionalTest']
}

task migratePostgresDatabase(type: GradleBuild, description: 'Migrates Postgres Database', group: 'liquibase') {
    //forward projectProperties
    startParameter.projectProperties = project.gradle.startParameter.projectProperties
    buildFile = './api/build.gradle'
    tasks = ['migratePostgresDatabase']
}

task printVersion {
    description 'Prints the version of this project'
    doLast {
        print project.version
    }
}

run {
    def debug = System.getProperty("debug")
    if (debug == 'true') {
        jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005']
    }
}
